plugins {
    id 'java'
    id 'checkstyle'
    id 'io.franzbecker.gradle-lombok'
    id 'com.github.ben-manes.versions'
    id 'com.palantir.git-version'
}

group = project.property('group')
description = project.property('description')

targetCompatibility = sourceCompatibility = JavaVersion.VERSION_21

repositories {
    mavenCentral()

    // Aikar
    maven { url = 'https://repo.aikar.co/content/groups/aikar/' }

    // CodeMC (nbt-api)
    maven { url = 'https://repo.codemc.org/repository/maven-public/' }

    // Paper
    // Paper artifacts are appearing in aikar's repo so we need to restrict them
    exclusiveContent {
        forRepository {
            maven {
                url "https://repo.papermc.io/repository/maven-public/"
            }
        }
        filter {
            includeGroup "com.mojang"
            includeGroup "io.papermc"
        }
    }

    // Sonatype (kyori)
    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots/' }

    // TeamTriumph
    maven { url = 'https://repo.triumphteam.dev/snapshots' }
}

lombok {
    version = '1.18.38'
    sha256 = ''
}

def details = versionDetails()

// Remove the "v" from a tag version
version = details.lastTag.substring(1) + (details.commitDistance > 0 ? '-dev' + details.commitDistance : '')

// Use the full tag
project.ext.fullVersion = details.lastTag + (details.commitDistance > 0 ? '-dev' + details.commitDistance : '')